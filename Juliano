Funcion valida=Validar(day, month, year)
	Definir valida Como Logico;
	//5. Se evalúa si el día es menor a 1 y vamos a la funcion del mes
	//10. Si era incorrecto, el número es mayor al 0 almacenado
	Si day<1 o day>DiaDelMes(month,year) Entonces
		//11. Entonces es falso
		valida=Falso;
	SiNo
		//12. De otra forma es verdadero
		valida=Verdadero;
	FinSi
FinFuncion
FuncioN bisiesto=EsBisiesto(A)
	Definir bisiesto Como Logico;
	//8. Se analiza si el año es bisiesto o no
	Si A mod 4=0 y A mod 100<>0 o A mod 400=0 Entonces
		bisiesto=Verdadero;
	SiNo
		bisiesto=Falso;
	FinSi
FinFuncion
Funcion dias=DiaDelMes(mes,an)
	Definir dias Como Entero;
	//6. Se analiza cuántos días tiene el mes y se guardan 
	Segun mes Hacer
		1, 3, 5, 7, 8, 10, 12:
			dias=31;
		4, 6, 9, 11:
			dias=30;
		2:
			//7. En caso de que el mes sea febrero se analiza si el año es bisiesto o no
			Si EsBisiesto(an) Entonces
				dias=29;
			SiNo
				dias=28;
			FinSi
		De Otro Modo:
			//9. En caso de que no correcponda a ninguno de los otros casos el día es 0
			dias=0;
	Fin Segun
FinFuncion
Funcion diajuliano=Calcular_Dia_Juliano(DIA,MES,AN)
	//15. Acá se calcula el día juliano
	Definir i, diajuliano Como Entero;
	//16. Se inicializa la variable para que se pueda hacer la suma
	diajuliano=0;
	//17. Se repite hasta que sea el mes anterior, para que no se cuenten todos los días del mes en el que está la fecha
	Para i=0 Hasta MES-1 Hacer
		//18. Vamos a la función, se usa el i porque así se sabe en que mes va contando
		//19. Entonces el resultado obtenido se le suma al día juliano, que va sumando los días de los meses que ya pasaron
		diajuliano=diajuliano+DiaDelMes(i,AN);
	FinPara
	//20. Y al día juliano se le va a sumar el día en el que está en el mes
	diajuliano=diajuliano+DIA;
FinFuncion
Funcion LeerFecha(d Por Referencia, m Por Referencia, a Por Referencia)
	Definir fechavalida Como Logico;
	//3. Se ingresa la fecha
	Repetir
		Escribir sin saltar "Ingrese el día";
		Leer d;
		Escribir sin saltar "Ingrese el mes";
		Leer m;
		Escribir sin saltar "Ingresa el año";
		Leer a;
		//4. Nos vamos a la función para ver si la fecha es correcta
		fechavalida=Validar(d,m,a);
		//13. Si la fecha no es válida se repite el proceso hasta que lo sea
		Si no fechavalida Entonces
			Escribir "Fecha incorrecta";
		FinSi
	Hasta Que fechavalida;
FinFuncion
Algoritmo juliano
	Escribir "Programa para definir el día juliano";
	//1. Se definen las variable, es decir, los días ingresados y el día juliano que se obtendrá
	Definir dd, mm, aa, dj Como Entero;
	//2. Se dirige a la función en donde se ingresa la fecha
	LeerFecha(dd,mm,aa);
	//14. Volamos a la función que sigue
	dj=Calcular_Dia_Juliano(dd,mm,aa);
	//21. El día juliano se almacena en dj y se va a mostrar
	Escribir "El día juliano es " dj;
FinAlgoritmo
